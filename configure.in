dnl  Process this file with autoconf to produce a configure script.
dnl  Copyright (C) 1998 John Harper <john@dcs.warwick.ac.uk>
dnl  $Id$
dnl
dnl  This file is part of Jade.
dnl
dnl  Jade is free software; you can redistribute it and/or modify it
dnl  under the terms of the GNU General Public License as published by
dnl  the Free Software Foundation; either version 2, or (at your option)
dnl  any later version.
dnl
dnl  Jade is distributed in the hope that it will be useful, but
dnl  WITHOUT ANY WARRANTY; without even the implied warranty of
dnl  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl  GNU General Public License for more details.
dnl
dnl  You should have received a copy of the GNU General Public License
dnl  along with Jade; see the file COPYING.  If not, write to
dnl  the Free Software Foundation, 675 Mass Ave, Cambridge, MA 02139, USA.

AC_REVISION($Revision$)

AC_INIT(rep-gtk.h)
AC_CONFIG_HEADER(config.h)

dnl Release versioning info
version="0.16.pre1"

OUT="gtk-2/types.la gtk-2/gtk.la"
AC_SUBST(OUT)

dnl Find the system type; this isn't crucial
AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_GCC_TRADITIONAL

dnl If using GCC and it doesn't look as though the cflags have been
dnl set explicitly, add some warning options. Turn off implicit-int
dnl warnings since the X11 includes on Solaris generate a lot of these
if test "x${GCC}" = "xyes" -a "x$CFLAGS" = "x-g -O2"; then
  CFLAGS="${CFLAGS} -Wall -Wmissing-prototypes -Wno-implicit-int"
fi

dnl Check for librep
AM_PATH_REP(0.13)

dnl Check for glib
PKG_CHECK_MODULES(GLIB, glib-2.0 >= 1.3)

dnl Check for GTK
PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 1.3,,AC_ERROR(need at least Gtk+ version 1.3))

PKG_CHECK_MODULES(GDK_PIXBUF, gdk-pixbuf-2.0 >= 1.3.11)

dnl Check for libgnome
AC_MSG_CHECKING([for GNOME libs])
AC_ARG_WITH(gnome,
 [  --with-gnome            Build GNOME wrappers
  --without-gnome         Don't wrap GNOME], [], [with_gnome=no])
if test "$with_gnome" != "no"; then
  GNOME_VERSION="`pkg-config --modversion libgnome-2.0`"
  if test $? -eq 0; then
    dnl XXX FIXME properly
    GNOME_LIBS="`pkg-config --libs libgnome-2.0`"
    GNOME_CFLAGS="`pkg-config --cflags libgnome-2.0`"
    GNOMEUI_LIBS="`pkg-config --libs libgnomeui-2.0`"
    GNOMEUI_CFLAGS="`pkg-config --cflags libgnomeui-2.0`"
    GNOME_CANVAS_LIBS="`pkg-config --libs libgnomecanvas-2.0`"
    GNOME_CANVAS_CFLAGS="`pkg-config --cflags libgnomecanvas-2.0`"
    AC_MSG_RESULT([$GNOME_VERSION])
    OUT="$OUT gtk-2/gnome-lib.la gtk-2/gnome-ui.la gtk-2/gnome-canvas.la"
    with_gnome=yes
  elif test "$with_gnome" = "yes"; then
    AC_MSG_ERROR([can't run pkg-config]);
  else
    with_gnome=no
  fi
fi
AC_SUBST(GNOME_VERSION)
AC_SUBST(GNOME_LIBS)
AC_SUBST(GNOME_CFLAGS)
AC_SUBST(GNOMEUI_LIBS)
AC_SUBST(GNOMEUI_CFLAGS)
AC_SUBST(GNOME_CANVAS_LIBS)
AC_SUBST(GNOME_CANVAS_CFLAGS)

dnl Check for libglade
AC_ARG_WITH(libglade,
 [  --with-libglade         Build libglade wrappers (dynamic GUI builder)
  --without-libglade      Don't wrap libglade], [], [with_libglade=yes])
if test "$with_libglade" = "yes"; then
  PKG_CHECK_MODULES(LIBGLADE, libglade-2.0 >= 1.99,
		    [OUT="$OUT gtk-2/libglade.la"])
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

dnl Check for cutting edge Gtk functions
saved_LIBS="$LIBS"
LIBS="$GTK_LIBS $saved_LIBS"
AC_CHECK_FUNCS(gdk_color_copy gtk_widget_peek_colormap gtk_type_get_info gtk_signal_set_class_function_full)
LIBS="$saved_LIBS"

dnl locale checks
AC_CHECK_FUNCS(setlocale)
AC_CHECK_HEADERS(locale.h)

dnl Nonstandard exported symbols
AC_SUBST(version)
AC_SUBST(repexecdir)
AC_SUBST(CPPFLAGS)
AC_SUBST(CFLAGS)
AC_SUBST(LIBS)

AC_DEFINE_UNQUOTED(REP_GTK_VERSION, "${version}")

AC_OUTPUT(Makefile rep-gtk.spec)

dnl Local variables:
dnl major-mode: sh-mode
dnl End:
